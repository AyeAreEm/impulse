@import "stdio.h";

int check_rule :: (int left int curr int right) {
    if (left = 1 and curr = 1 and right = 1) {
        return 0;
    } orif (left = 1 and curr = 1 and right = 0) {
        return 1;
    } orif (left = 1 and curr = 0 and right = 1) {
        return 1;
    } orif (left = 1 and curr = 0 and right = 0) {
        return 0;
    } orif (left = 0 and curr = 1 and right = 1) {
        return 1;
    } orif (left = 0 and curr = 1 and right = 0) {
        return 1;
    } orif (left = 0 and curr = 0 and right = 1) {
        return 1;
    } else {
        return 0;
    }
}

_ display :: (int cell) {
    if (cell = 1) {
        print("#");
    } else {
        print(" ");
    }
}

_ copy :: (^int dest ^int src int len) {
    loop (i < len) [+] {
        dest[i]: src[i];
    }
}

_ main :: () {
    int gens: 100;
    [80]int cells: |1 1 0 0 1 0 1 0|;
    [80]int next_gen;

    loop (gen < gens) [+] {
        loop (i < 80) [+] {
            int l_index;
            if (i = 0) {
                l_index: 79;
            } else {
                l_index: [i - 1];
            }
            int left: cells[l_index];

            int curr: cells[i];

            int r_index: [[i + 1] % 80];
            int right: cells[r_index];

            int new_cell: check_rule(left curr right);
            next_gen[i]: new_cell;
            display(new_cell);
        }

        println("");

        copy(cells next_gen 80);
    }
}
